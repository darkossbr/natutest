plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations.configureEach {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$fabric_loader_version")
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url = "https://api.modrinth.com/maven" }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://cursemaven.com" }
    maven {
        name = 'Terraformers'
        url = 'https://maven.terraformersmc.com/'
    }
    maven { url = "https://maven.fabricmc.net/" } // adicionado por segurança
}

dependencies {
    minecraft "net.minecraft:minecraft:${rootProject.minecraft_version}"

    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.21.1:${rootProject.parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation("software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}")
    modImplementation include("maven.modrinth:midnightlib:${project.midnightlib_fabric_version}-fabric")
    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

    modApi include("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_fabric_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version
    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    archiveFileName.set("${project.archives_name}-${project.mod_version}+fabric-${minecraft_version}.jar")
    inputFile.set(shadowJar.archiveFile)
    dependsOn shadowJar
    archiveClassifier = "null"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/resources']
        }
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_name + "-" + project.name
            from components.java
        }
    }
    repositories {
        // Adicione destinos de publicação se necessário
    }
}
